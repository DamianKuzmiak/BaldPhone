/*
 * Copyright 2019 Uriah Shaul Mandel
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'

android {
    namespace "com.bald.uriah.baldphone"
    compileSdk 34
    defaultConfig {
        applicationId "com.bald.uriah.baldphone"
        minSdk 21
        //noinspection ExpiredTargetSdkVersion
        targetSdk 28
        versionCode 105
        versionName "14.1.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [fullApp: "true", recentCalls: "true"]

    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            versionNameSuffix "-debug"
        }
    }

    flavorDimensions "updating_mechanism"
    productFlavors {
        baldUpdates {
            dimension "updating_mechanism"
        }
        fDroid {
            dimension "updating_mechanism"
        }
        gPlay {
            dimension "updating_mechanism"
            applicationIdSuffix ".gp"
            manifestPlaceholders = [fullApp: "true", recentCalls: "false"]
        }
        gPlayRecentCalls {
            dimension "updating_mechanism"
            applicationIdSuffix ".gprc"
            manifestPlaceholders = [fullApp: "false", recentCalls: "true"]
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_17
        sourceCompatibility JavaVersion.VERSION_17

        // java.util.stream for API below 24,
        // java.time, java.util.stream for API below 26
        coreLibraryDesugaringEnabled true
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    // AndroidX - Core & UI
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.core:core:1.13.1' // 1.31.1 version is max for android-34
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'  // [Long Term] Migrate away from it.
    implementation 'androidx.recyclerview:recyclerview:1.3.2'  // 1.3.2 version is max for android-34

    // AndroidX - Room (Database)
    implementation 'androidx.room:room-runtime:2.7.2'
    annotationProcessor 'androidx.room:room-compiler:2.7.2'

    // Google - Material Design
    implementation 'com.google.android.material:material:1.13.0'

    // Third-party Libraries
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'com.github.bumptech.glide:glide:5.0.5'
    annotationProcessor 'com.github.bumptech.glide:compiler:5.0.5'
    implementation 'com.github.nisrulz:lantern:1.1.2'  // Version 2.0.0 was unavailable from JitPack
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'net.danlew:android.joda:2.10.6'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

    // Only for tests
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'com.github.blainepwnz:AndroidContacts:1.14.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation 'junit:junit:4.13'
}
